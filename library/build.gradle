apply plugin: "com.android.library"
apply plugin: "com.jfrog.bintray"
apply plugin: "maven"
apply plugin: "maven-publish"

ext
{
  jacksonVersion="2.9.4"
}

android
{
  compileSdkVersion 27

  defaultConfig
  {
    minSdkVersion 14
    targetSdkVersion 27

    versionCode 17
    versionName "2.9.0-SNAPSHOT"
  }

  buildTypes
  {
    release
    {
      minifyEnabled false
      zipAlignEnabled true
      proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
    }
  }
}

dependencies
{
  // Android
  implementation ("com.android.support:appcompat-v7:27.1.1")

  // Smart&Soft
  api("com.smartnsoft:droid4me:2.9.0-SNAPSHOT")

  // Third party libraries
  api("com.fasterxml.jackson.core:jackson-core:$jacksonVersion")
  api("com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion")
  api("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")
}

task sourcesJar(type: Jar) {
  classifier = "sources"
  from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
  title = "droid4me.ext API"
  description "Javadoc for the droid4me.ext framework"
  source = android.sourceSets.main.java.srcDirs

  options.charSet = "UTF-8"
  options.overview = "src/main/javadoc/overview.html"
  options.links = ["http://docs.oracle.com/javase/7/docs/api/", "http://d.android.com/reference/"]

  classpath += project.files(project.android.getBootClasspath().join(File.pathSeparator))
  failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = "javadoc"
  from javadoc.destinationDir
  // options.encoding = 'UTF-8'
}

artifacts
{
  archives javadocJar
  archives sourcesJar
}

def pomConfig = {
  licenses
  {
    license
    {
      name "MIT License (MIT)"
      url "https://opensource.org/licenses/MIT"
      distribution "repo"
    }
  }
}

publishing
{
  publications
  {
    ReleasePublication(MavenPublication)
    {
      artifact sourcesJar
      artifact javadocJar
      groupId "com.smartnsoft"
      artifactId "droid4me.ext"
      version android.defaultConfig.versionName

      pom.withXml
      {
        def root = asNode()
        root.appendNode("description", "droid4me.ext is an extension of the droid4me framework.")
        root.appendNode("name", "droid4me.ext")
        root.appendNode("url", "https://github.com/smartnsoft/droid4me.ext")
        root.children().last() + pomConfig
      }
    }
  }
}

//https://gist.github.com/bugs84/b7887fb5d7f9f2d484b8
publishing.publications.all
{
  pom.withXml
  {
    asNode().dependencies."*".findAll()
    {
      it.scope.text() == "runtime" && project.configurations.compile.allDependencies.find { dep ->
        dep.name == it.artifactId.text()
      }
    }.each
    {
      it.scope*.value = "compile"
    }
  }
}

bintray
{
  user = bintrayUsername
  key = bintrayKey

  publications = ["ReleasePublication"]

  pkg
  {
    repo = "maven"
    name = "droid4me.ext"
    userOrg = "smartnsoft"
    licenses = ["MIT"]
    vcsUrl = "https://github.com/smartnsoft/droid4me.ext.git"
    publish = true

    version
    {
      name = android.defaultConfig.versionName
      desc = "droid4me.ext is an extension of the droid4me framework."
      released  = new Date()
      vcsTag = "droid4me.ext-" + android.defaultConfig.versionName
    }
  }
}

uploadArchives
{
  repositories
  {
    mavenDeployer
    {
      repository(url: "https://api.bintray.com/maven/smartnsoft/maven/droid4me.ext/;publish=1")
      {
        authentication(userName: bintrayUsername, password: bintrayKey)
      }

      snapshotRepository(url: nexusSnapshotUrl)
      {
        authentication(userName: nexusUsername, password: nexusPassword)
      }

      pom.project
      {
        name "droid4me.ext"
        groupId "com.smartnsoft"
        artifactId "droid4me.ext"
        version android.defaultConfig.versionName
        packaging "aar"
      }
    }
  }
}
